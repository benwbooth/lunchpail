pragma ComponentBehavior: Bound

import QtQuick

// The same identity/artwork contract is used by the wall and both animated paths.
Item {
    id: card
    required property var library
    required property int index
    required property string gameId
    required property string gameTitle
    required property string gameCanonicalTitle
    required property string gamePlatform
    required property bool gameLocal
    required property bool gameDownloadable
    required property int gameDatabaseId
    required property double gameMediaId
    property bool selected: false
    property bool wheel: false
    property color ink: "#f4f7fb"
    property color muted: "#8d99aa"
    property color accent: "#ffb454"
    property color panel: "#182230"
    property int artworkRevision: library.media_revision
    readonly property bool favorite: {
        library.favorite_revision
        return library.is_favorite(gameId)
    }
    readonly property bool favoriteBusy: {
        library.favorite_pending_count
        return library.favorite_pending(gameId)
    }
    readonly property url artwork: {
        artworkRevision
        // Generic artwork lookup may substitute a portrait cover when a logo
        // is missing. The wheel needs a logo or its readable title fallback.
        return wheel ? library.exact_artwork_url(gameMediaId, "clear-logo")
                     : library.artwork_url(gameMediaId, "box-front")
    }
    signal activated(int index)

    // The viewport owns fetching. Delegate creation and role changes must not
    // enqueue the same image repeatedly while the wheel is moving.

    Rectangle {
        anchors.fill: frame
        anchors.margins: -5
        radius: 17
        color: "transparent"
        border.color: card.accent
        border.width: 2
        opacity: card.selected && !card.wheel ? 0.45 : 0
        Behavior on opacity { NumberAnimation { duration: 220 } }
    }
    Rectangle {
        id: frame
        anchors.fill: parent
        anchors.margins: 9
        radius: 12
        color: card.wheel ? (card.selected ? Qt.rgba(0.03, 0.06, 0.1, 0.72) : "transparent") : card.panel
        border.color: card.wheel ? (card.selected ? Qt.rgba(card.accent.r, card.accent.g, card.accent.b, 0.6) : "transparent") : card.selected ? card.accent : Qt.rgba(1, 1, 1, 0.14)
        border.width: card.selected ? 2 : 1
        scale: card.wheel || card.selected ? 1 : hover.hovered ? 1.025 : 0.97
        Behavior on scale { NumberAnimation { duration: 180; easing.type: Easing.OutCubic } }
        Image {
            id: cover
            anchors.fill: parent
            anchors.margins: card.wheel ? 14 : 5
            anchors.bottomMargin: card.wheel ? 14 : 52
            source: card.artwork
            sourceSize.width: card.wheel ? 640 : 480
            sourceSize.height: card.wheel ? 240 : 640
            asynchronous: true
            cache: true
            mipmap: !card.wheel
            retainWhileLoading: true
            fillMode: Image.PreserveAspectFit
            opacity: status === Image.Ready ? 1 : 0
            Behavior on opacity { NumberAnimation { duration: 200 } }
        }
        Text {
            anchors.centerIn: cover
            width: cover.width - 20
            visible: cover.status !== Image.Ready
            text: card.wheel ? card.gameTitle : card.gameTitle.charAt(0).toUpperCase()
            color: card.ink
            font.pixelSize: card.wheel ? 28 : 70
            font.weight: Font.Black
            minimumPixelSize: 14
            fontSizeMode: Text.Fit
            wrapMode: Text.WordWrap
            horizontalAlignment: Text.AlignHCenter
            height: cover.height
            verticalAlignment: Text.AlignVCenter
        }
        Text {
            visible: !card.wheel
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.bottom: parent.bottom
            anchors.margins: 12
            height: 34
            text: card.gameTitle
            color: card.ink
            font.pixelSize: 16
            minimumPixelSize: 12
            fontSizeMode: Text.Fit
            font.weight: Font.DemiBold
            wrapMode: Text.WordWrap
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
        }
        Rectangle {
            anchors.right: parent.right
            anchors.top: parent.top
            anchors.margins: 9
            width: 8; height: 8; radius: 4
            visible: card.gameLocal && !card.wheel
            color: "#5ee391"
        }
        Rectangle {
            visible: card.wheel && card.selected
            anchors { left: parent.left; verticalCenter: parent.verticalCenter; leftMargin: 7 }
            width: 4; height: parent.height * 0.42; radius: 2; color: card.accent
        }
        FavoriteButton {
            anchors.left: parent.left
            anchors.top: parent.top
            anchors.margins: 9
            visible: !card.wheel
            favorite: card.favorite
            busy: card.favoriteBusy
            gameTitle: card.gameTitle
            onToggleRequested: favorite => card.library.set_favorite(card.gameId, favorite)
        }
    }
    HoverHandler { id: hover }
    TapHandler { onTapped: card.activated(card.index) }
    Accessible.role: Accessible.ListItem
    Accessible.name: gameTitle + ", " + gamePlatform
    Accessible.selected: selected
    Accessible.onPressAction: activated(index)
}
